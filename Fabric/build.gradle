plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base {
    archivesName = mod_id + "-" + mod_version + "-" + minecraft_version_group + "-FABRIC"
}

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.06.26@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

    modImplementation "curse.maven:enchantment-descriptions-250419:4756854"
    modImplementation "curse.maven:bookshelf-228525:4771035"

    modImplementation "curse.maven:inventory-hud-forge-357540:4699628"

    modImplementation include("fuzs.extensibleenums:extensibleenums-fabric:${extensible_enums_version}")

    implementation project(":Common")
}

processResources {
    from project(":Common").sourceSets.main.resources

    filteringCharset "UTF-8"

    inputs.property "version", mod_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "minecraft_version_range", fabric_minecraft_version_range

    filesMatching("fabric.mod.json") {
        expand  "version": mod_version,
                "minecraft_version": minecraft_version,
                "minecraft_version_range": fabric_minecraft_version_range
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)

    it.options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    source(project(":Common").sourceSets.main.allJava)
}

// Ideally this project would use Gradle from the root project
tasks.register('wrapper', Wrapper) {
    gradleVersion = '8.1.1'
}

jar.finalizedBy('remapJar')